import com.github.gradle.node.npm.task.NpmInstallTask
import com.github.gradle.node.npm.task.NpmTask

plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.5'
	id 'io.spring.dependency-management' version '1.1.7'
    id 'org.openapi.generator' version '7.14.0'
    id "com.github.node-gradle.node" version "5.0.0"
}

group = 'com.voidsamuraj'
version = '0.0.1-SNAPSHOT'
description = 'Job Requirements Analyzer'

// Folder na pobrany JSON
def openApiJsonDir = "$projectDir/openapi"
def openApiJsonFile = "$openApiJsonDir/remote-ok-openapi.json"
java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

repositories {
	mavenCentral()
}

dependencies {
    runtimeOnly 'com.h2database:h2'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.openapitools:jackson-databind-nullable:0.2.7'
    implementation 'org.springframework.boot:spring-boot-starter-websocket'
    implementation 'org.slf4j:slf4j-api:2.0.12'
    implementation 'ch.qos.logback:logback-classic:1.5.19'
    implementation 'org.jsoup:jsoup:1.21.2'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    compileOnly 'org.projectlombok:lombok'
	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.projectlombok:lombok'
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.boot:spring-boot-testcontainers'
	testImplementation 'org.springframework.graphql:spring-graphql-test'
	testImplementation 'org.testcontainers:junit-jupiter'
	testImplementation 'org.testcontainers:postgresql'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

}

openApiGenerate {
    generatorName = "java"
    library = "webclient"
    configOptions =[
            serializableModel: "true",
            dateLibrary: "java8",
            serializationLibrary: "jackson"
    ]
    inputSpec = file("src/main/resources/openapi/remote-ok-openapi.json").absolutePath.replace('\\','/')
    outputDir = file("build/generated/openapi").absolutePath.replace('\\','/')
    apiPackage = "com.voidsamuraj.HireLens.client.remoteok.api"
    modelPackage = "com.voidsamuraj.HireLens.client.remoteok.dto"
    invokerPackage = "com.voidsamuraj.HireLens.client.remoteok"
}
node {
    version = '22.19.0'
    npmVersion = '10.9.3'
    download = true
    nodeModulesDir = file('src/main/HireLens-front')
}

def envFrontFile = file('src/main/HireLens-front/.env')
def envFrontVars = [:]

if (envFrontFile.exists()) {
    envFrontFile.eachLine { line ->
        if (line && !line.startsWith('#')) {
            def (key, value) = line.split('=', 2)
            envFrontVars[key.trim()] = value.trim()
        }
    }
}

tasks.register('npmInstall_my', NpmInstallTask) {
    workingDir.set(file('src/main/HireLens-front'))
}

tasks.register('npmRunBuild', NpmTask) {
    dependsOn(tasks.named('npmInstall_my'))
    workingDir.set(file('src/main/HireLens-front'))

    def envFile = file('src/main/HireLens-front/.env')
    def props = new Properties()
    if (envFile.exists()) {
        envFile.withInputStream { stream ->
            stream.eachLine { line ->
                if (line && !line.startsWith("#")) {
                    def (k, v) = line.split('=', 2)
                    props.setProperty(k.trim(), v.trim())
                }
            }
        }
    }

    def viteUrl = System.getenv("REACT_APP_API_URL") ?: props.getProperty("REACT_APP_API_URL", "http://localhost:8080")

    environment.put("VITE_API_URL", viteUrl)
    args = ['run', 'build']
}

tasks.register('copyReactBuild', Copy) {
    dependsOn tasks.named('npmRunBuild')
    from('src/main/HireLens-front/dist')
    into('build/resources/main/static')
}
tasks.named('processResources') {
    dependsOn 'copyReactBuild'
}

tasks.named('build').configure {
    dependsOn 'copyReactBuild'
}


sourceSets {
    main {
        java {
            srcDir "build/generated/openapi/src/main/java"
        }
    }
}
compileJava.dependsOn tasks.openApiGenerate

tasks.named('compileJava') {
    dependsOn 'openApiGenerate'
}
tasks.named('test') {
	useJUnitPlatform()
}
